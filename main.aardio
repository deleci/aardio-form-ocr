import win.ui;
import fonts.fontAwesome;
import win.clip;
import fsys;
import win.dlg.message;
/*DSG{{*/
mainForm = win.form(text="表格o文字识别工具";right=499;bottom=319;bgcolor=16777215;border="none";max=false)
mainForm.add(
bk={cls="bk";left=0;top=0;right=500;bottom=32;bgcolor=16777215;forecolor=8882055;linearGradient=0;z=1};
bk2={cls="bk";text="表格o文字识别工具";left=46;top=4;right=256;bottom=26;align="left";color=4934475;z=3};
bk3={cls="bk";text='\uF083';left=16;top=6;right=38;bottom=24;color=4934475;font=LOGFONT(h=-18;name='FontAwesome');z=4};
btnAbout={cls="plus";text='\uF2DC';left=465;top=36;right=501;bottom=72;border={radius=6};color=2368548;dr=1;font=LOGFONT(h=-19;name='FontAwesome');notify=1;z=8};
btnClear={cls="plus";text='\uF1F8';left=419;top=36;right=455;bottom=72;border={radius=6};color=2368548;font=LOGFONT(h=-29;name='FontAwesome');notify=1;z=10};
btnCopy={cls="plus";text='\uF24D';left=374;top=36;right=410;bottom=72;border={radius=6};color=2368548;font=LOGFONT(h=-29;name='FontAwesome');notify=1;z=7};
btnOcrW={cls="plus";text='\uF1C2';left=328;top=36;right=364;bottom=72;border={radius=6};color=2368548;font=LOGFONT(h=-29;name='FontAwesome');notify=1;z=13};
btnOcrX={cls="plus";text='\uF1C3';left=282;top=36;right=318;bottom=72;border={radius=6};color=2368548;font=LOGFONT(h=-29;name='FontAwesome');notify=1;z=6};
btnOpen={cls="plus";text='\uF03E';left=237;top=36;right=273;bottom=72;border={radius=6};color=2368548;font=LOGFONT(h=-29;name='FontAwesome');notify=1;z=11};
btnPrScrn={cls="plus";text='\uF125';left=191;top=36;right=227;bottom=72;border={radius=6};color=2368548;font=LOGFONT(h=-24;name='FontAwesome');notify=1;z=5};
chkOnTop={cls="plus";text="置顶窗口";left=9;top=39;right=93;bottom=69;align="left";dl=1;dt=1;font=LOGFONT(h=-14);iconStyle={align="left";font=LOGFONT(h=-15;name='FontAwesome')};iconText='\uF0C8';notify=1;textPadding={left=16};z=9};
chkSave={cls="plus";text="保存XLSX";left=94;top=39;right=178;bottom=69;align="left";dl=1;dt=1;font=LOGFONT(h=-14);iconStyle={align="left";font=LOGFONT(h=-15;name='FontAwesome')};iconText='\uF0C8';notify=1;textPadding={left=16};z=12};
edit={cls="plus";left=8;top=75;right=493;bottom=313;acceptfiles=1;align="left";bgcolor=14803425;border={right=1;color=-4137792};edge=1;editable=1;font=LOGFONT(h=-14;name='宋体');hscroll=1;multiline=1;repeat="scale";vscroll=1;z=2}
)
/*}}*/

import win.ui.atom;
var atom,hwnd = mainForm.atom("5cba911e-2dbf-4df3-a7b0-fa4a27997815.leci.ocr");
if(!atom){
	win.setForeground(hwnd);
	win.quitMessage();
	return;
}

global.onError = function( err,over ){ 
    if(!over){
        win.msgboxErr ("Uh-oh! Something happened.")
    }
} 

import win.ui.simpleWindow2;
var simpleWindow2 = win.ui.simpleWindow2(mainForm);

import style
mainForm.chkOnTop.skin(style.checkBox)
mainForm.chkSave.skin(style.checkBox)

mainForm.btnPrScrn.skin(style.icon)
mainForm.btnOpen.skin(style.icon)
mainForm.btnOcrX.skin(style.icon)
mainForm.btnOcrW.skin(style.icon)
mainForm.btnCopy.skin(style.icon)
mainForm.btnClear.skin(style.icon)
mainForm.btnAbout.skin(style.icon)

import win.ui.tooltip; 
var tooltipCtrl = win.ui.tooltip( mainForm );
tooltipCtrl.addTool(mainForm.btnPrScrn,"屏幕截图")
tooltipCtrl.addTool(mainForm.btnOpen,"打开图片")
tooltipCtrl.addTool(mainForm.btnOcrX,"识别表格")
tooltipCtrl.addTool(mainForm.btnOcrW,"识别文字")
tooltipCtrl.addTool(mainForm.btnCopy,"复制文本")
tooltipCtrl.addTool(mainForm.btnClear,"清空内容")
tooltipCtrl.addTool(mainForm.btnAbout,"关于程序")

import fsys.table;
var pathSetting=io.appData("\lecisoft\表格o文字识别工具\setting.table")
settings = fsys.table(pathSetting)
settings.load(); 

mainForm.ak=settings.api_key
mainForm.sk=settings.secret_key
var hasKeys = mainForm.ak && mainForm.sk
if !hasKeys {
	mainForm.edit.text='api_key:xxxxxxxxxxxxxxxxxxxxxxxx\nsecret_key:xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx'
	mainForm.btnClear.text='\uF25D'
	tooltipCtrl.addTool(mainForm.btnClear,"保存访问令牌")
	mainForm.btnOcrX.disabled=true
	mainForm.btnOcrW.disabled=true
}

//mainForm.chkOnTop.checked=true;
chkOnTop=function(){
	if(mainForm.chkOnTop.checked){
		win.setTopmost(mainForm.hwnd,true)
	} else {
		win.setTopmost(mainForm.hwnd,false)	
	}
}
chkOnTop()

mainForm.chkOnTop.oncommand = function(id,event){
	chkOnTop()
}
mainForm.btnAbout.oncommand = function(id,event){
	var frmAbout = win.loadForm("\dlg\about.aardio");
	var aboutme = frmAbout.doModal();
}
mainForm.btnCopy.oncommand = function(id,event){
	win.clip.write(mainForm.edit.text);
	mainForm.msgOk("完成：文本内容已复制到剪贴板",500)
}

mainForm.btnClear.oncommand = function(id,event){
	if owner.text='\uF25D' {
		var txt=mainForm.edit.text;
		if string.trim(txt) {
			txt=string.replace(txt,"<@:@>","=")
			var lines=string.splitEx(string.trim(txt),'\r\n')
			var ak,sk
			for(i=1;#lines;1){
				if string.indexOf(lines[i],"api_key"){
					ak=string.splitEx(lines[i],"=")[2]:""
					ak=string.replace(ak,"[\s\p]","")
				}
				if string.indexOf(lines[i],"secret_key"){
					sk=string.splitEx(lines[i],"=")[2]:""
					sk=string.replace(sk,"[\s\p]","")
				}
			}

			if ak=="xxxxxxxxxxxxxxxxxxxxxxxx"||sk=="xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx"||#ak!=24||#sk!=32 {
				mainForm.msgErr("错误：请按要求填写访问令牌信息",1000);
				return ;
			} 
			mainForm.edit.text='访问令牌注册成功！\n你可以开始使用啦！'
			settings.api_key=ak
			settings.secret_key=sk
			mainForm.ak=settings.api_key
			mainForm.sk=settings.secret_key
			settings.save()
			hasKeys=true
			mainForm.btnClear.text='\uF1F8'
			tooltipCtrl.addTool(mainForm.btnClear,"清空文本框内容")
			mainForm.btnOcrX.disabled=false
		}
	}else{
		mainForm.edit.background=null
		mainForm.edit.text = "";
	}
}

import fsys.dlg;
mainForm.btnOpen.oncommand = function(id,event){
	var path = fsys.dlg.open('图片文件 jpg png bmp jpeg|*.jpg;*.png;*.jpeg;*.bmp||',,,mainForm);
	if(path){
		mainForm.edit.text = path;
	}
}
mainForm.edit.wndproc = {
    [0x233/*_WM_DROPFILES*/] = function(hwnd,message,wParam,lParam){ 
    	var files = win.getDropFile(wParam);
		var suffix = fsys.getExtensionName(files[1])	
		select(suffix) {
			case "bmp","jpg","jpeg","png" {
				mainForm.edit.text=files[1];
        	}else{
				mainForm.msgErr("错误：所选文件格式错误",800);
				return; 
        	}
    	}
    }
}

var dll = raw.loadDll($"\lib\PrScrn.dll");
mainForm.btnPrScrn.oncommand = function(id,event){
	mainForm.show(false);
	thread.delay(60)
	dll.PrScrn();
	thread.delay(60)
    hbmp = win.clip.readBitmap();
    
    if(!hbmp){
		mainForm.msgErr("错误：请先截图或复制图像!",1000)
    }else{	
    	mainForm.edit.background=hbmp;
    }
	mainForm.show(true);
}

import gdip.bitmap
import crypt.bin
mainForm.getImg=function(fi){
    var img
    if io.exist(fi) {
		img=crypt.bin.encodeBase64(string.load(fi))
	} else {
		var hbmp = win.clip.readBitmap();
		if hbmp {
    		var buffer = gdip.bitmap(hbmp).saveToBuffer();
    		img=crypt.bin.encodeBase64(buffer)
    		win.clip.writeBitmap()
		}
	}
	return img
}
getOcrResult=function(mainForm,api,fi){
	var img=mainForm.getImg(fi)
	if !img {
		mainForm.btnOcrX.disabledText=null;
		mainForm.btnOcrW.disabledText=null;
		mainForm.edit.text="错误：没有发现图片数据"
		return ; 
	}
    import baiduocr;
    var http = baiduocr.client(
        mainForm.ak,mainForm.sk
    ) 
    if(! http.auth() ){
    	mainForm.msgErr("错误：识别接口认证失败!",2000)
        mainForm.edit.text="错误：识别接口认证失败"
        mainForm.btnOcrX.disabledText=null;
		mainForm.btnOcrW.disabledText=null;
        return ; 
    }
    var ocr = http.api(api);
    var result = ocr( 
        image = img;
    )
    mainForm.edit.background=null
    return result
}

mainForm.btnOcrX.oncommand = function(id,event){
	mainForm.btnOcrX.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'} 
	var fi=io.exist(mainForm.edit.text):""
	var tpath=io.splitpath(fi)
	var fo=io.joinpath(io._exedir,tpath.name+"_识别结果.xlsx")
	mainForm.edit.text= ""
	var apiX="https://aip.baidubce.com/rest/2.0/ocr/v1/table"
    var result=thread.invokeAndWait(getOcrResult,mainForm,apiX,fi)
    if(result) {
        if result.table_num<1 {
            mainForm.btnOcrX.disabledText=null; 
            mainForm.edit.text='错误：截图中不包含表格\n提示：本按钮只识别表格内容'
            return ; 
        }
		var body=result.tables_result[1].body
		var nItem=#body
		var nRow =body[nItem].row_start
		var nCol =body[nItem].col_start
		var rows={}
		for i=0;nRow;1 {
			var row={}
			for j=0;nCol;1{
				for k,item in body {
					if item.row_start==i&&item.col_start==j {
						table.push(row,item.words:"")
						break(1)
					}
				}
			}
			table.push(rows,string.join(row,'\t'))
		}
	
		var csv=string.join(rows,'\n'); 
		mainForm.edit.text=csv
		
		if mainForm.chkSave.checked {
			try{
				import dotNet;
				dotNet.reference({
  					["unvell.ReoGrid"] = $"\lib\unvell.ReoGrid.dll";
  					["DotNetZip"] = $"\lib\DotNetZip.dll"
				});
				dotNet.import("unvell.ReoGrid")
				dotNet.import("DotNetZip")
				var book = unvell.ReoGrid.ReoGridControl();
				var sheet = book.CurrentWorksheet;
				for k,item in body {
					sheet.SetCellData(item.row_start,item.col_start,item.words)
				}
				book.Save(fo)
				thread.delay(50)
				book=null
				mainForm.msgOk("完成：识别结果已保存",500)
			}
			catch(e){
				mainForm.edit.text='错误：系统缺乏必要在 .Net组件！\n提示：请复制文本框内容手工粘贴。'
			}
		}
    } 
	mainForm.btnOcrX.disabledText = null
}

mainForm.btnOcrW.oncommand = function(id,event){
	var fi=mainForm.edit.text
	mainForm.edit.text= ""
	mainForm.btnOcrW.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'} 
	var apiW="https://aip.baidubce.com/rest/2.0/ocr/v1/accurate_basic"
    var result=thread.invokeAndWait(getOcrResult,mainForm,apiW,fi)
    if result {
        var txt=""
	    if result.words_result_num {
            for (i=1;result.words_result_num;1){
                txt+=result.words_result[i].words+'\n'
            }
            mainForm.edit.text= txt;
        }else{
            mainForm.edit.text= "错误：没有识别到文本内容！";
        }
        mainForm.btnOcrW.disabledText = null
    }

}

import win.ui.menu;
import win.util.tray;
mainForm.tray = win.util.tray(mainForm);
mainForm.tray.tip = '表格o文字识别工具\n[Ctrl+J ] 显示软件窗口\n[Ctrl+Q] 直接关闭软件' 
mainForm.onTrayMessage = {
	[0x205/*_WM_RBUTTONUP*/  ] = function(wParam){ 
		import win.ui.menu; 
		var popmenu = win.ui.popmenu(mainForm);
		win.setForeground(mainForm.hwnd);
		popmenu.add('退出',function(id){
			mainForm.onClose = null;
			mainForm.close();
		});
		popmenu.popup();
	};
	[0x202/*_WM_LBUTTONUP*/] = function(wParam){
		mainForm.show(true);
	}; 
}

mainForm.onMinimize = function(lParam){
	mainForm.show(false);
	return true;
}
mainForm.onClose = function(hwnd,message,wParam,lParam){
	mainForm.show(false);
	return true;
}

var hkId;
mainForm.unreghotkey(hkId);
hkId = mainForm.reghotkey(
    function(id,mod,vk){
        mainForm.onClose = null;
        mainForm.close()
    } ,2/*_MOD_CONTROL*/,'Q'#
);
hkId = mainForm.reghotkey(
    function(id,mod,vk){
        win.showForeground(mainForm.hwnd);
    } ,2/*_MOD_CONTROL*/,'J'#
);

mainForm.show();
return win.loopMessage();
